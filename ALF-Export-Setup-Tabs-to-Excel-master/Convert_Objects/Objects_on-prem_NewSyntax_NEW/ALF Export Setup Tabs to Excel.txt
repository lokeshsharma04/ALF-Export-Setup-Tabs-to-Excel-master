OBJECT Report 50126 ALF Export Setup Tabs to Excel
{
  OBJECT-PROPERTIES
  {
    Date=20180220D;
    Time=095013.843T;
    Modified=true;
    Version List=1.0;
  }
  PROPERTIES
  {
    ProcessingOnly=true;
    OnPreReport=BEGIN
                  TempExcelBuffer.DELETEALL;
                  RowNo := 1;
                  ColNo := 1;
                  EnterCell(RowNo,ColNo,FORMAT(COMPANYPROPERTY.URLNAME+';'+TENANTID+';'+USERID+';'+FORMAT(TIME)),TempExcelBuffer."Cell Type"::Text,true,false,true);
                  StartRowNo := 3;
                END;

    OnPostReport=BEGIN
                   GlobalRecRef.OPEN(DATABASE::"General Ledger Setup");
                   GlobalRecRef.FINDFIRST;
                   FillHeader(StartRowNo,GlobalRecRef);
                   StartRowNo := FillLine(StartRowNo,GlobalRecRef) + 2;
                   GlobalRecRef.CLOSE;

                   GlobalRecRef.OPEN(DATABASE::"Sales & Receivables Setup");
                   GlobalRecRef.FINDFIRST;
                   FillHeader(StartRowNo,GlobalRecRef);
                   StartRowNo := FillLine(StartRowNo,GlobalRecRef) + 2;
                   GlobalRecRef.CLOSE;

                   GlobalRecRef.OPEN(DATABASE::"Purchases & Payables Setup");
                   GlobalRecRef.FINDFIRST;
                   FillHeader(StartRowNo,GlobalRecRef);
                   StartRowNo := FillLine(StartRowNo,GlobalRecRef) + 2;
                   GlobalRecRef.CLOSE;

                   GlobalRecRef.OPEN(DATABASE::"Inventory Setup");
                   GlobalRecRef.FINDFIRST;
                   FillHeader(StartRowNo,GlobalRecRef);
                   StartRowNo := FillLine(StartRowNo,GlobalRecRef) + 2;
                   GlobalRecRef.CLOSE;

                   TempExcelBuffer.CreateNewBook(SheetName);
                   TempExcelBuffer.WriteSheet(SheetName,'','');
                   TempExcelBuffer.CloseBook;
                   TempExcelBuffer.OpenExcel;
                   TempExcelBuffer.GiveUserControl; // please comment this line for Cloud app
                 END;

    UseRequestPage=false;
  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempExcelBuffer@1000 : TEMPORARY Record "Excel Buffer";
      RowNo@1001 : Integer;
      ColNo@1002 : Integer;
      GlobalRecRef@1003 : RecordRef;
      StartRowNo@1004 : Integer;
      SheetName@1005 : TextConst 'ENU="ENU=ALF Export Setup Tabs to Excel"';

    LOCAL PROCEDURE EnterCell@1(RowNo@1000 : Integer;ColumnNo@1001 : Integer;CellValue@1002 : Text[250];CellType@1003 : 'Number,Text,Date,Time';Bold@1004 : Boolean;Italic@1005 : Boolean;Underline@1006 : Boolean);
    BEGIN
      TempExcelBuffer.INIT;
      TempExcelBuffer.VALIDATE("Row No.",RowNo);
      TempExcelBuffer.VALIDATE("Column No.",ColumnNo);
      TempExcelBuffer."Cell Value as Text" := CellValue;
      TempExcelBuffer."Cell Type" := CellType;
      TempExcelBuffer.Bold := Bold;
      TempExcelBuffer.Italic := Italic;
      TempExcelBuffer.Underline := Underline;
      TempExcelBuffer.INSERT;
    END;

    LOCAL PROCEDURE FillHeader@4(StartRowNo@1001 : Integer;LocalRecRef@1002 : RecordRef);
    VAR
      LocalFieldRef@1000 : FieldRef;
    BEGIN
      RowNo := StartRowNo;
      ColNo := 1;
      EnterCell(RowNo,ColNo,LocalRecRef.CAPTION + ' ('+FORMAT(LocalRecRef.NUMBER)+')',TempExcelBuffer."Cell Type"::Text,true,true,true);
      ColNo := 2;
      EnterCell(RowNo,ColNo,'VALUE',TempExcelBuffer."Cell Type"::Text,true,true,true);
      ColNo := 3;
      EnterCell(RowNo,ColNo,'CLASS',TempExcelBuffer."Cell Type"::Text,true,true,true);
      ColNo := 4;
      EnterCell(RowNo,ColNo,'OPTION',TempExcelBuffer."Cell Type"::Text,true,true,true);
      ColNo := 1;
      for RowNo := (1 + StartRowNo) to (LocalRecRef.FIELDCOUNT + StartRowNo) do begin
        LocalFieldRef := LocalRecRef.FIELDINDEX(RowNo - StartRowNo);
        EnterCell(RowNo,ColNo,LocalFieldRef.CAPTION + ' ('+FORMAT(LocalFieldRef.NUMBER)+')',TempExcelBuffer."Cell Type"::Text,true,false,false);
      end;
    END;

    LOCAL PROCEDURE FillLine@6(StartRowNo@1001 : Integer;LocalRecRef@1002 : RecordRef) : Integer;
    VAR
      LocalFieldRef@1000 : FieldRef;
    BEGIN
      for RowNo := (1 + StartRowNo) to (LocalRecRef.FIELDCOUNT + StartRowNo) do begin
        LocalFieldRef := LocalRecRef.FIELDINDEX(RowNo - StartRowNo);
        ColNo := 2;
        EnterCell(RowNo,ColNo,FORMAT(LocalFieldRef.VALUE),TempExcelBuffer."Cell Type"::Text,false,false,false);
        ColNo := 3;
        EnterCell(RowNo,ColNo,FORMAT(LocalFieldRef.CLASS),TempExcelBuffer."Cell Type"::Text,false,false,false);
        ColNo := 4;
        EnterCell(RowNo,ColNo,FORMAT(LocalFieldRef.OPTIONCAPTION),TempExcelBuffer."Cell Type"::Text,false,false,false);
      end;
      exit(RowNo);
    END;

    BEGIN
    {
      //Please add these properties for Cloud app
      //Caption = 'ALF Export Setup Tabs to Excel';
      //UsageCategory = ReportsAndAnalysis;
      //ApplicationArea = All,Basic,Suite;
    }
    END.
  }
  RDLDATA
  {
  }
}

